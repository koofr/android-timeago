plugins {
    id 'com.android.library'
    id 'maven-publish'
}

version = "1.0.12"

android {
    namespace = "net.koofr.android.timeago"

    compileSdkVersion = 36

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
        versionCode 10012
        versionName version
    }

    buildTypes {
    }
}

Properties local = new Properties()
local.load(project.rootProject.file("local.properties").newDataInputStream())

tasks.register("sourceJar", Jar) {
    archiveClassifier = "sources"
    from android.sourceSets.main.java.srcDirs
}

publishing {
    repositories {
        maven {
            name = 'koofr'
            url = local.getProperty('repo.url')
            credentials {
                username = local.getProperty('repo.user')
                password = local.getProperty('repo.key')
            }
        }
    }
    publications {
        koofr(MavenPublication) {
            groupId = 'net.koofr'
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/android-timeago-release.aar")
            pom {
                name = 'android-timeago'
                description = 'A Java library for formatting ages'
                url = 'https://github.com/koofr/android-timeago'
                licenses {
                    license {
                        name = 'Apache  License 2.0'
                        url = 'https://github.com/koofr/android-timeago/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'jkmcnk'
                        name = 'Jaka Močnik'
                        email = 'jaka@koofr.net'
                    }
                    developer {
                        id = 'bancek'
                        name = 'Luka Zakrajšek'
                        email = 'luka@koofr.net'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/koofr/android-timeago.git'
                    developerConnection = 'scm:git:https://github.com/koofr/android-timeago.git'
                    url = 'https://github.com/koofr/android-timeago'
                }
            }
        }
    }
}

dependencies {
}
